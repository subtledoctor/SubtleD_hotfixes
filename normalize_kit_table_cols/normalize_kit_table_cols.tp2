BACKUP ~normalize_kit_table_cols/backup~
AUTHOR ~SubtleD, with help from Argent77~


//do stuff__________________________________________________________________________
//
BEGIN ~Make Kit Ability Tables Uniform~

DEFINE_PATCH_FUNCTION normalize_table BEGIN

  SET num_added = 0
  COUNT_2DA_COLS num_cols
  READ_2DA_ENTRIES_NOW table 1
  READ_2DA_ENTRY_FORMER table 1 0 default
  FOR (row = 0; row < table; ++row) BEGIN
    READ_2DA_ENTRY_FORMER table row 0 line
    FOR (col = 1; col < num_cols && VARIABLE_IS_SET $table(~%row%~ ~%col%~); ++col) BEGIN
      READ_2DA_ENTRY_FORMER table row col v
      TEXT_SPRINT line ~%line% %v%~
    END
    // fill in missing data
    PATCH_IF (row = 2) BEGIN  // header
      FOR (; col < num_cols - 1; ++col) BEGIN
        TEXT_SPRINT line ~%line% %col%~
        SET num_added += 1
      END
    END ELSE PATCH_IF (row > 2) BEGIN // content
      FOR (; col < num_cols; ++col) BEGIN
        TEXT_SPRINT line ~%line% %default%~
        SET num_added += 1
      END
    END
    TEXT_SPRINT $lines(~%row%~) ~%line%%LNL%~
    SET $lines(~%row%~ ~size~) = STRING_LENGTH $lines(~%row%~)
  END

  // write normalized table to buffer
  PATCH_IF (num_added > 0) BEGIN
    DELETE_BYTES 0 BUFFER_LENGTH
    FOR (row = 0 pos = 0; row < table; ++row) BEGIN
      SET size = $lines(~%row%~ ~size~)
      INSERT_BYTES pos size
      WRITE_ASCIIE pos $lines(~%row%~) (size)
      SET pos += size
    END
  END
  
  PRETTY_PRINT_2DA

END

ACTION_DEFINE_ASSOCIATIVE_ARRAY kit_tables BEGIN
clabfi01	=> 1
clabfi02	=> 1
clabfi03	=> 1
clabfi04	=> 1
clabfi05	=> 1
clabfi06	=> 1
clabpa01	=> 1
clabpa02	=> 1
clabpa03	=> 1
clabpa04	=> 1
clabpa05	=> 1
clabrn01	=> 1
clabrn02	=> 1
clabrn03	=> 1
clabrn04	=> 1
clabrn05	=> 1
clabpr01	=> 1
clabpr02	=> 1
clabpr03	=> 1
clabpr04	=> 1
clabdr01	=> 1
clabdr02	=> 1
clabdr03	=> 1
clabdr04	=> 1
clabth01	=> 1
clabth02	=> 1
clabth03	=> 1
clabth04	=> 1
clabma01	=> 1
clabma02	=> 1
clabma03	=> 1
clabma04	=> 1
clabma05	=> 1
clabma06	=> 1
clabma07	=> 1
clabma08	=> 1
clabma09	=> 1
END

COPY_EXISTING ~kitlist.2da~ ~override~
  COUNT_2DA_COLS cols
  READ_2DA_ENTRIES_NOW ~r2en_kitlist~ cols
  FOR (row = 0; row < r2en_kitlist; ++row) BEGIN
    READ_2DA_ENTRY_FORMER ~r2en_kitlist~ row 5 kit_table
    PATCH_IF (FILE_EXISTS_IN_GAME ~%kit_table%.2da~) BEGIN
      SPRINT $kit_tables(~%kit_table%~)~1~
    END
  END
BUT_ONLY

ACTION_PHP_EACH kit_tables AS table => ind BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~%table%.2da~) BEGIN
    COPY_EXISTING ~%table%.2da~ ~override~
    LPF normalize_table END
  END
END