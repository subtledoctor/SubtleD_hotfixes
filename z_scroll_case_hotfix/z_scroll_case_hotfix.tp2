BACKUP ~z_scroll_case_hotfix/backup~
AUTHOR ~SubtleD~


//do stuff__________________________________________________________________________
//
BEGIN ~fix scroll cases~

COPY_EXISTING_REGEXP GLOB ~^.+\.sto$~ ~override~
  READ_LONG 0x2c buy_off   ELSE 0
  READ_LONG 0x30 buy_num   ELSE 0
  SET keys = 0
  FOR (index = 0 ; index < buy_num ; ++index) BEGIN
    READ_LONG (buy_off + (index * 0x04)) item
    PATCH_IF (item = 11) BEGIN // scrolls
      SET keys = 1
    END
    PATCH_IF (item = 8) BEGIN // keys
      SET keys = 2
      SET index = buy_num // kills loop
    END
  END
  PATCH_IF (keys = 1) BEGIN // if scrolls but no keys
    INSERT_BYTES buy_off 0x04
      WRITE_LONG buy_off 8
    WRITE_LONG 0x30 (buy_num + 1)
    PATCH_FOR_EACH offset IN 0x34 0x4c 0x70 BEGIN // adjust offsets if needed, thankfully the same in v9, v1, and v1.1
      READ_LONG offset off
      PATCH_IF off > buy_off BEGIN
        WRITE_LONG offset (off + 0x04)
      END
    END
  END
BUT_ONLY

