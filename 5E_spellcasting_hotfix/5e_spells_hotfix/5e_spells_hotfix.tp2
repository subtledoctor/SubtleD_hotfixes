BACKUP ~5e_spells_hotfix/backup~
AUTHOR ~SubtleD~


//do stuff__________________________________________________________________________
//
BEGIN ~fix 5e spells~


COPY_EXISTING_REGEXP GLOB ~^D5Z[1-9][0-9][0-9]I.SPL$~ ~override~
  LPF ALTER_EFFECT INT_VAR match_opcode = 326 opcode = 146 parameter1 = 0 parameter2 = 1 STR_VAR match_resource = ~d5zspld~ END
  LPF ALTER_EFFECT INT_VAR match_opcode = 326 opcode = 146 parameter1 = 0 parameter2 = 1 STR_VAR match_resource = ~d5zspla~ END
BUT_ONLY

ACTION_IF (FILE_CONTAINS_EVALUATED (~splstate.ids~ ~D5_SEMI_ARCANE~)) BEGIN
  COPY_EXISTING ~splstate.ids~ ~override~
	COUNT_2DA_COLS cols
	READ_2DA_ENTRIES_NOW rows cols
	FOR (row = 1; row < rows; ++row) BEGIN
	  READ_2DA_ENTRY_FORMER rows row 1 ~state_name~
	  READ_2DA_ENTRY_FORMER rows row 0 ~state_ind~
	  PATCH_IF (~%state_name%~ STRING_EQUAL_CASE ~D5_SEMI_ARCANE~) BEGIN
		SET semi_spont_arcane = %state_ind%
	  END
	END
  BUT_ONLY
END

ACTION_IF !(VARIABLE_IS_SET %semi_spont_arcane%) BEGIN
  LAF d5_resolve_state STR_VAR new_state_id = ~D5_SEMI_ARCANE~ RET new_state_ind END
  OUTER_SET semi_spont_arcane = %new_state_ind%
END

ACTION_IF (FILE_CONTAINS_EVALUATED (~splstate.ids~ ~D5_SEMI_DIVINE~)) BEGIN
  COPY_EXISTING ~splstate.ids~ ~override~
	COUNT_2DA_COLS cols
	READ_2DA_ENTRIES_NOW rows cols
	FOR (row = 1; row < rows; ++row) BEGIN
	  READ_2DA_ENTRY_FORMER rows row 1 ~state_name~
	  READ_2DA_ENTRY_FORMER rows row 0 ~state_ind~
	  PATCH_IF (~%state_name%~ STRING_EQUAL_CASE ~D5_SEMI_DIVINE~) BEGIN
		SET semi_spont_divine = %state_ind%
	  END
	END
  BUT_ONLY
END

ACTION_IF !(VARIABLE_IS_SET %semi_spont_divine%) BEGIN
  LAF d5_resolve_state STR_VAR new_state_id = ~D5_SEMI_DIVINE~ RET new_state_ind END
  OUTER_SET semi_spont_divine = %new_state_ind%
END

COPY_EXISTING ~D5ZSPLD.SPL~ ~override~
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 318 insert_point = 0 target = 1 parameter1 = %semi_spont_divine% parameter2 = 111 timing = 0 duration = 1 STR_VAR resource = ~d5zspld~ END
IF_EXISTS BUT_ONLY

COPY_EXISTING ~D5ZSPLA.SPL~ ~override~
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 318 insert_point = 0 target = 1 parameter1 = %semi_spont_arcane% parameter2 = 111 timing = 0 duration = 1 STR_VAR resource = ~d5zspla~ END
IF_EXISTS BUT_ONLY

ACTION_IF (FILE_EXISTS_IN_GAME ~qdtnb_arcanist.qd~) BEGIN
  COPY_EXISTING ~kit.ids~ ~override~
	COUNT_2DA_COLS cols
	READ_2DA_ENTRIES_NOW rows cols
	FOR (row = 1; row < rows; ++row) BEGIN
	  READ_2DA_ENTRY_FORMER rows row 0 ~code~
	  READ_2DA_ENTRY_FORMER rows row 0 ~kit~
	  PATCH_IF ~%kit%~ STRING_EQUAL_CASE ~D5_ARCANIST~ BEGIN
	    SET arcanist_code = %code%
	  END
	END  
  BUT_ONLY
  APPEND ~splprot.2da~ ~D5_KIT_IS%TAB%152%TAB%-1%TAB%1~ UNLESS ~D5_KIT_IS~
  COPY_EXISTING ~splprot.2da~ ~override~
	COUNT_2DA_COLS cols
	READ_2DA_ENTRIES_NOW rows cols
	FOR (row = 1; row < rows; ++row) BEGIN
	  READ_2DA_ENTRY_FORMER rows row 0 ~stat~
	  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~D5_KIT_IS~ BEGIN
	    SET kit_is_row = %row%
	  END
	END
  BUT_ONLY
  COPY_EXISTING ~d5zprpd.spl~ ~override~
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 326 target = 1 parameter1 = %arcanist_code% parameter2 = %kit_is_row% timing = 1 STR_VAR resource = ~d5zltac~ END
  IF_EXISTS BUT_ONLY
END
